/*
 * Copyright (c) 2020 Cryptic Mushroom and contributors
 * This file belongs to the Midnight mod and is licensed under the terms and conditions of Cryptic Mushroom. See
 * https://github.com/Cryptic-Mushroom/The-Midnight/blob/rewrite/LICENSE.md for the full license.
 *
 * Last updated: 2021 - 7 - 5
 */

/*
 * GradleWorks for ForgeGradle 4/3
 * Written by Jonathing
 * Version 3.3.2
 *
 * This gradle file contains additional instructions for publishing builds of this mod to maven repositories.
 */

// shoves all of the sources in a jar
task sourcesJar(type: Jar, dependsOn: classes) {
    if (!(project.hasProperty('use_forgegradle3') && "${use_forgegradle3}".toString() == 'true')) {
        archiveClassifier.set('sources')
    } else {
        classifier = 'sources'
    }
    from sourceSets.main.allSource
}

// shoves all of the sources and deobf compiled classes in a jar
task deobfJar(type: Jar, dependsOn: classes) {
    if (!(project.hasProperty('use_forgegradle3') && "${use_forgegradle3}".toString() == 'true')) {
        archiveClassifier.set('deobf')
    } else {
        classifier = 'deobf'
    }
    from sourceSets.main.java.sourceDirectories
    from sourceSets.main.output
}

// generates the javadocs and shoves them in a jar
task javadocJar(type: Jar) {
    from javadoc
    if (!(project.hasProperty('use_forgegradle3') && "${use_forgegradle3}".toString() == 'true')) {
        archiveClassifier.set('javadoc')
    } else {
        classifier = 'javadoc'
    }
}

// Ensures that the mod is built before it is published to the maven
publish {
    dependsOn 'build'
}

publishing {
    publications {
        // Get the main jar, sources jar, and javadoc jars for publishing
        mavenJava(MavenPublication) {
            groupId = "${maven_group}".toString()
            artifactId = "${maven_artifact_id}".toString()
            version = "${mc_version}-${version}".toString()

            artifact reobfFile // main jar
            artifact sourcesJar // sources jar
            artifact javadocJar // javadoc jar
        }
    }
    repositories {
        // Test Maven repository located in the "mcmodsrepo" folder
        maven {
            name "TestMaven"
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}
